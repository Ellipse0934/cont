include std.cn
include platform.cn

memory file 2049

var tape [3000] int
var head int

var inp int
memory out 1

var len int
var loop_skip int
var loop_stack [100] ptr
var loop_ptr int

"++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++++++.>.+++.------.--------.>+.>." file cpy_buf

var *i ptr
file !*i

while *i @8 0 != do
  *i @8 bind i:
    if loop_skip do
      if i '[' == do
        *loop_skip inc
      else if i ']' == do
        *loop_skip dec
      end end 
    else
      if i '+' == do
        head tape *[] inc
      else if i '-' == do
        head tape *[] dec
      else if i '>' == do
        *head inc
      else if i '<' == do
        *head dec
      else if i '.' == do
        1 head tape *[] puts
      else if i ',' == do
        #if platform Platform.fasm_x86_64_linux ==;
          1 *inp STDOUT read drop
          inp head tape *[] !
        #endif
        #if platform Platform.wat64 ==;
          "Cannot use input in wasm version" println
          1 exit
        #endif
      else if i '[' == do
        if head tape [] do
          *i loop_ptr loop_stack *[] !
          *loop_ptr inc
        else
          *loop_skip inc
        end
      else if i ']' == do
        if head tape [] do
          loop_ptr 1 - loop_stack [] !*i
        else
          *loop_ptr dec
        end
      end end end end end end end end
    end
  end
  **i inc
end
:
Hello World!
